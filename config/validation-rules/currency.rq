PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema:  <http://schema.org/>
PREFIX spin:    <http://spinrdf.org/spin#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
  [] a spin:ConstraintViolation ;
    spin:violationRoot ?s ;
    spin:violationPath schema:currency ;
    spin:invalidValue ?currency ;
    rdfs:label "Invalid currency code."@en , 
      "Neplatný kód měny."@cs ;
    rdfs:comment "Use ISO 4217 codes: http://en.wikipedia.org/wiki/ISO_4217 (e.g., 'EUR' for Euro, 'USD' for U.S. dollars)"@en ,
      "Použijte kódy z ISO 4217: http://cs.wikipedia.org/wiki/ISO_4217 (např. 'CZK' pro české koruny, 'EUR' pro Eura)"@cs
    .
}
WHERE {
  GRAPH ?validatedGraph {
    ?s schema:currency ?currency .
  }
  BIND (str(?currency) AS ?currencyStr)
  FILTER NOT EXISTS {
    GRAPH <http://data.damepraci.cz/resource/currency-codes> {
      [] skos:inScheme <http://data.damepraci.cz/resource/concept-scheme/currency-codes> ;
        skos:notation ?currencyStr .
    }
  }
}
