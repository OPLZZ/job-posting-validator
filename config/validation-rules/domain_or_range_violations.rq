# TODO: Handle rdfs:subPropertyOf?

PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema:  <http://schema.org/>
PREFIX spin:    <http://spinrdf.org/spin#>

CONSTRUCT {
  [] a spin:ConstraintViolation ;
    spin:violationRoot ?s ;
    spin:violationPath ?p1, ?p2 ;
    spin:invalidValue ?o ;
    rdfs:label ?violationLabelEN, 
      ?violationLabelCS
    .
}
WHERE {
  {
    SELECT DISTINCT ?s ?p1 ?p2 ?o ?violationLabelEN ?violationLabelCS
    WHERE {
      GRAPH ?validatedGraph { 
        ?s ?p1 ?o .
        ?o ?p2 ?o2 .
      }
      # Check if rdfs:range and rdfs:domain are available for the properties.
      GRAPH <http://vocab.damepraci.eu> {
        ?p1 schema:rangeIncludes [] .
        ?p2 schema:domainIncludes [] .
      }
      # Check if rdfs:range and rdfs:domain matches.
      FILTER NOT EXISTS {
        GRAPH <http://vocab.damepraci.eu> {
          {
            ?p1 schema:rangeIncludes/rdfs:subClassOf*/^schema:domainIncludes ?p2 .
          } UNION {
            ?p2 schema:domainIncludes [
              owl:unionOf ?unionOf
            ] .
            ?unionOf rdf:rest*/rdf:first/rdfs:subClassOf*/^schema:rangeIncludes ?p1 .
          } UNION {
            ?p1 schema:rangeIncludes [
              owl:unionOf ?unionOf
            ] .
            ?unionOf rdf:rest*/rdf:first/rdfs:subClassOf*/^schema:domainIncludes ?p2 .
          } UNION {
            ?p1 schema:rangeIncludes/owl:unionOf/rdf:rest*/rdf:first/rdfs:subClassOf* ?class .
            ?p2 schema:domainIncludes/owl:unionOf/rdf:rest*/rdf:first/rdfs:subClassOf* ?class .
          }
        }
      }
      BIND (STRLANG(CONCAT(
        "The value of property <",
        str(?p1),
        "> is either not allowed or it is not to be used with the property <",
        str(?p2),
        ">."
      ), "en") AS ?violationLabelEN)
      BIND (STRLANG(CONCAT(
        "Hodnota vlastnosti <",
        str(?p1),
        "> buď není přípustná nebo nelze použít s vlastností <",
        str(?p2),
        ">."
      ), "cs") AS ?violationLabelCS)
    }
  }
}
