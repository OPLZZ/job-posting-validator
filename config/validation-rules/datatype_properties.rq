PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX spin:    <http://spinrdf.org/spin#>

CONSTRUCT {
  [] a spin:ConstraintViolation ;
    spin:violationRoot ?s ;
    spin:violationPath ?datatypeProperty ;
    spin:invalidValue ?o ;
    rdfs:label ?violationLabelEN, ?violationLabelCS
    .
}
WHERE {
  GRAPH <http://vocab.damepraci.eu> {
    ?datatypeProperty a owl:DatatypeProperty .
  }
  GRAPH ?validatedGraph {
    ?s ?datatypeProperty ?o .
    FILTER (!isLiteral(?o))
    BIND (STRLANG(CONCAT(
      "The value of the property <",
      str(?datatypeProperty),
      "> must be a literal (text), not an object (\"",
      str(?o),
      "\")."
    ), "en") AS ?violationLabelEN)
    BIND (STRLANG(CONCAT(
      "Hodnotou vlastnosti <",
      str(?datatypeProperty),
      "> musí být literál (text), ne objekt (\"",
      str(?o),
      "\")."
    ), "cs") AS ?violationLabelCS)
  }
}
